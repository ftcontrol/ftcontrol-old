plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.bylazar.panelslimelightproxy'

    compileSdkVersion 35

    defaultConfig {
        minSdk 24
    }


    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation project(':ftcontrol')

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation("org.nanohttpd:nanohttpd:2.3.1")
}

def libName = "limelight-proxy"
def libVersion = "0.1.0"
def jarFileName = "${libName}-${libVersion}.jar"

tasks.register('copyLimelightProxyJar', Copy) {
    dependsOn("build")

    def libsDir = file("${rootProject.projectDir}/TeamCode/libs")
    def stashDir = file("${rootProject.projectDir}/TeamCode/.lib-versions")
    def aarPath = layout.buildDirectory.file("outputs/aar/LimelightProxy-release.aar").get().asFile

    doFirst {
        libsDir.mkdirs()
        libsDir.listFiles()?.findAll {
            it.name.endsWith(".jar") && it.name.startsWith(libName)
        }?.each {
            println "Deleting old jar: ${it.name}"
            it.delete()
        }
    }

    from(zipTree(aarPath)) {
        include "classes.jar"
        rename { jarFileName }
    }

    into(libsDir)

    doLast {
        def finalJar = new File(libsDir, jarFileName)

        stashDir.mkdirs()
        def backupFile = new File(stashDir, jarFileName)
        finalJar.withInputStream { input ->
            backupFile.withOutputStream { output ->
                output << input
            }
        }

        println "Copied ${jarFileName} to libs/"
        println "Backed up as ${jarFileName} to .jar-stash/"
    }
}